; PureRGBnote: ADDED: CHANGED: a bunch of this code was modified to allow for multiple items to be picked up at once from an item ball.

PickUpItemQuantity::
	jr PickUpItemCommon

PickUpItem::
	ld c, 1 ; default quantity
PickUpItemCommon:
	push bc
	call EnableAutoTextBoxDrawing

	ldh a, [hSpriteIndex]
	ld b, a
	ld hl, wToggleableObjectList
.toggleableObjectsListLoop
	ld a, [hli]
	cp $ff
	jr z, .noObject
	cp b
	jr z, .isToggleable
	inc hl
	jr .toggleableObjectsListLoop

.isToggleable
	ld a, [hl]
	pop bc ; obtain c which is either set to 1 if we called PickUpItem, or an arbitrary quantity otherwise
	ld b, a

	ld hl, wMapSpriteExtraData
	ldh a, [hSpriteIndex]
	dec a
	add a
	ld d, 0
	ld e, a
	add hl, de
	push bc
	ld b, [hl]
	call GiveItem
	pop bc
	ld hl, NoMoreRoomForItemText
	jr nc, .print
	push bc
	ld c, b
;;;;;;;;;; PureRGBnote: CHANGED: in certain maps hidable items use a different set of flags than everywhere else, needed more space for flags.
	CheckEvent EVENT_IN_EXTRA_TOGGLEABLE_OBJECTS_MAP
	jr nz, .hideExtra
	call HideObject
	jr .continue
.hideExtra
	call HideExtraObject
.continue
;;;;;;;;;;
	ld a, 1
	ld [wDoNotWaitForButtonPressAfterDisplayingText], a
	and a
	pop bc
	ld a, c
	cp 1
	ld hl, FoundItemText
	jr z, .print
.multiItemPickup
	add NUMBER_CHAR_OFFSET ; index of first number character in charmap (assumes c must be 0-9)
	ld [wTempStore1], a
	ld a, '@'
	ld [wTempStore2], a
	ld hl, FoundMultipleItemText
.print
	rst _PrintText
	ret
.noObject
	pop bc
	ret

FoundMultipleItemText:
	text_far _FoundMultipleItemText
	sound_get_item_1
	text_end

FoundItemText:
	text_far _FoundItemText
	sound_get_item_1
	text_end

NoMoreRoomForItemText:
	text_far _NoMoreRoomForItemText
	text_end
