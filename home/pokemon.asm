DrawHPBar::
	homecall _DrawHPBar
	ret


; loads pokemon data from one of multiple sources to wLoadedMon
; loads base stats to wMonHeader
; INPUT:
; [wWhichPokemon] = index of pokemon within party/box
; [wMonDataLocation] = source
; 00: player's party
; 01: enemy's party
; 02: current box
; 03: daycare
; OUTPUT:
; [wCurPartySpecies] = pokemon ID
; wLoadedMon = base address of pokemon data
; wMonHeader = base address of base stats
LoadMonData::
	jpfar LoadMonData_

;OverwritewMoves::
; Write c to [wMoves + b]. Unused.
;	ld hl, wMoves
;	ld e, b
;	ld d, 0
;	add hl, de
;	ld a, c
;	ld [hl], a
;	ret

LoadFlippedFrontSpriteByMonIndex::
	ld a, 1
	ld [wSpriteFlipped], a

LoadFrontSpriteByMonIndex::
	push hl
	ld a, [wPokedexNum]
	push af
	ld a, [wCurPartySpecies]
	ld [wPokedexNum], a
	call IndexToPokedex
	ld hl, wPokedexNum
	ld a, [hl]
	pop bc
	ld [hl], b
	and a
	pop hl
	cp NUM_POKEMON + 1
	jr c, .validDexNumber   ; dex >#151 invalid
.invalidDexNumber
	; This is the so-called "Rhydon trap" or "Rhydon glitch"
	; to fail-safe invalid dex numbers
	; (see https://glitchcity.wiki/wiki/Rhydon_trap
	; or https://bulbapedia.bulbagarden.net/wiki/Rhydon_glitch)
	ld a, RHYDON
	ld [wCurPartySpecies], a
	ret
.validDexNumber
	push hl
	ld de, vFrontPic
	call LoadMonFrontSprite
	pop hl
	ldh a, [hLoadedROMBank]
	push af
	ld a, BANK(CopyUncompressedPicToHL)
	call SetCurBank
	xor a
	ldh [hStartTileID], a
	call CopyUncompressedPicToHL
	xor a
	ld [wSpriteFlipped], a
	pop af
	jp SetCurBank


;;;;;;;;;; PureRGbnote: ADDED: code that remaps channel 8 of armored mewtwo's cry if it's passed to this function
PlayCry::
; Play monster a's cry.
	push af
	call GetCryData
	rst _PlaySound
	pop af
	cp ARMORED_MEWTWO
	jr nz, .wait
	callfar RemapArmoredMewtwoCry
.wait
	jp WaitForSoundToFinish
;;;;;;;;;;

GetCryData::
; Load cry data for monster a.
	push de
	ld e, a
	callfar GetCryData2
	ld a, d
	pop de
	ret

DisplayPartyMenu::
	ldh a, [hTileAnimations]
	push af
	xor a
	ldh [hTileAnimations], a
	call GBPalWhiteOut
	call ClearSprites
	call PartyMenuInit
	call DrawPartyMenu
	jp HandlePartyMenuInput

GoBackToPartyMenu::
	ldh a, [hTileAnimations]
	push af
	xor a
	ldh [hTileAnimations], a
	call PartyMenuInit
	call RedrawPartyMenu
	jp HandlePartyMenuInput

PartyMenuInit::
	ld a, 1 ; hardcoded bank
	call BankswitchHome
	call LoadHpBarAndStatusTilePatterns
	call DisableTextDelay
	xor a ; PLAYER_PARTY_DATA
	ld [wMonDataLocation], a
	ld [wMenuWatchMovingOutOfBounds], a
	ld hl, wTopMenuItemY
	inc a
	ld [hli], a ; top menu item Y
	xor a
	ld [hli], a ; top menu item X
	ld a, [wPartyAndBillsPCSavedMenuItem]
	push af
	ld [hli], a ; current menu item ID
	inc hl
	ld a, [wPartyCount]
	and a ; are there more than 0 pokemon in the party?
	jr z, .storeMaxMenuItemID
	dec a
; if party is not empty, the max menu item ID is ([wPartyCount] - 1)
; otherwise, it is 0
.storeMaxMenuItemID
	ld [hli], a ; max menu item ID
	push hl
	callfar GetPartyMenuWatchedKeys
	pop hl
.next
	ld a, d
	ld [hli], a ; menu watched keys
	pop af
	ld [hl], a ; old menu item ID
	ret

HandlePartyMenuInput::
	ld a, 1
	ld [wMenuWrappingEnabled], a
	ld a, $40
	ld [wPartyMenuAnimMonEnabled], a
	call HandleMenuInput_
	call PlaceUnfilledArrowMenuCursor
	ld b, a
	xor a
	ld [wPartyMenuAnimMonEnabled], a
	ld a, [wCurrentMenuItem]
	ld [wPartyAndBillsPCSavedMenuItem], a
	bit B_PAD_SELECT, b
	jr z, .notSelect
	push af
	ld a, SFX_PRESS_AB
	rst _PlaySound
	ld a, [wMenuItemToSwap]
	and a
	jr nz, .swap
	pop af
	inc a ; [wMenuItemToSwap] counts from 1
	ld [wMenuItemToSwap], a
	jr HandlePartyMenuInput
.swap
	pop af
.notSelect
	call EnableTextDelay
	ld a, [wMenuItemToSwap]
	and a
	jp nz, .swappingPokemon
	pop af
	ldh [hTileAnimations], a
	bit B_PAD_B, b
	jr nz, .noPokemonChosen
	ld a, [wPartyCount]
	and a
	jr z, .noPokemonChosen
	ld a, [wCurrentMenuItem]
	ld [wWhichPokemon], a
	ld hl, wPartySpecies
	ld b, 0
	ld c, a
	add hl, bc
	ld a, [hl]
	ld [wCurPartySpecies], a
	ld [wBattleMonSpecies2], a
	call BankswitchBack
	and a
	ret
.noPokemonChosen
	call BankswitchBack
	scf
	ret
.swappingPokemon
	bit B_PAD_B, b
	jr z, .handleSwap
; cancel swap if the B button was pressed
	farcall ErasePartyMenuCursors
	xor a
	ld [wMenuItemToSwap], a
	ld [wPartyMenuTypeOrMessageID], a
	call RedrawPartyMenu
	jr HandlePartyMenuInput
.handleSwap
	ld a, [wCurrentMenuItem]
	ld [wWhichPokemon], a
	farcall SwitchPartyMon
	jp HandlePartyMenuInput

DrawPartyMenu::
	ld hl, DrawPartyMenu_
	jr DrawPartyMenuCommon

RedrawPartyMenu::
	callfar ResetPartyAnimation ; mechanicalpennote: ADDED: new code for rendering party menu icons
	ld hl, RedrawPartyMenu_

DrawPartyMenuCommon::
	ld b, BANK(RedrawPartyMenu_)
	rst _Bankswitch
	ret

; prints a pokemon's status condition
; INPUT:
; de = address of status condition
; hl = destination address
PrintStatusCondition::
	push de
	dec de
	dec de ; de = address of current HP
	ld a, [de]
	ld b, a
	dec de
	ld a, [de]
	or b ; is the pokemon's HP zero?
	pop de
	jr nz, PrintStatusConditionNotFainted
; if the pokemon's HP is 0, print "KO"
	ld_hli_a_string "KO"
	and a
	ret

PrintStatusConditionNotFainted::
	homecall_sf PrintStatusAilment
	ret

; function to print pokemon level, leaving off the ":L" if the level is at least 100
; INPUT:
; hl = destination address
; [wLoadedMonLevel] = level
PrintLevel::
	ld a, '<LV>' ; ":L" tile ID
PrintLevelArbitraryTile::
	ld [hli], a
	ld c, 2 ; number of digits
	ld a, [wLoadedMonLevel] ; level
	cp 100
	jr c, PrintLevelCommon
; if level at least 100, write over the ":L" tile
	dec hl
	inc c ; increment number of digits to 3
	jr PrintLevelCommon

; prints the level without leaving off ":L" regardless of level
; INPUT:
; hl = destination address
; [wLoadedMonLevel] = level
PrintLevelFull::
	ld a, '<LV>' ; ":L" tile ID
	ld [hli], a
	ld c, 3 ; number of digits
	ld a, [wLoadedMonLevel] ; level

PrintLevelCommon::
	ld [wTempByteValue], a
	ld de, wTempByteValue
	ld b, LEFT_ALIGN | 1 ; 1 byte
	jp PrintNumber

;GetwMoves::
; Unused. Returns the move at index a from wMoves in a
;	ld hl, wMoves
;	ld c, a
;	ld b, 0
;	add hl, bc
;	ld a, [hl]
;	ret

; copies the base stat data of a pokemon to wMonHeader
; INPUT:
; [wCurSpecies] = pokemon ID
GetMonHeader::
	ldh a, [hLoadedROMBank]
	push af
	ld a, BANK(BaseStats)
	call SetCurBank
	call GetMonHeader2 ; moved to BaseStats bank because it was getting unwieldy
	pop af
	jp SetCurBank

; copy party pokemon's name to wNameBuffer
GetPartyMonName2::
	ld a, [wWhichPokemon] ; index within party
	ld hl, wPartyMonNicks

; this is called more often
GetPartyMonName::
	push hl
	push bc
	call SkipFixedLengthTextEntries ; add NAME_LENGTH to hl, a times
	ld de, wNameBuffer
	push de
	ld bc, NAME_LENGTH
	rst _CopyData
	pop de
	pop bc
	pop hl
	ret

; input a = starter pokemon id
; output a = charmander = 1, squirtle = 2, bulbasaur = 3
StarterToPartyID::
	ld b, 1
	cp STARTER1
	jr z, .done
	inc b
	cp STARTER2
	jr z, .done
	inc b
.done
	ld a, b
	ret

AreLearnsetsEnabled::
	CheckEvent FLAG_LEARNSETS_DISABLED
	jr nz, .no
	CheckEvent EVENT_GOT_MOVEDEX
	jr z, .no
	; nz required to reach here
	ret
.no
	xor a
	ret
	
IndexToPokedex::
	homecall _IndexToPokedex
	ret
